plugins {
  id 'java-library'
}

sourceCompatibility = '17'
targetCompatibility = '17'
version = '3.5'

dependencies {
  implementation project(':rcrs-server')

  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'javax.activation:activation:1.1.1'
  implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
  implementation 'log4j:log4j:1.2.17'
  implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.0'

  testImplementation 'junit:junit:4.12'
}

repositories {
  mavenCentral()

  maven {
    url = 'http://sourceforge.net/projects/jsi/files/m2_repo'
    allowInsecureProtocol = true
  }
}

javadoc.options.encoding = 'UTF-8'
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

test {
  useJUnit()
}

clean.doFirst {
  delete file( new File( rootDir, 'bin' ) )
  delete file( new File( rootDir, 'build' ) )
}

[compileJava, compileTestJava].each {
  it.options.encoding = 'UTF-8'
  it.options.compilerArgs += [ '-Xlint:unchecked', '-Xlint:deprecation' ]
}

jar {
  manifest {
    attributes 'Main-Class': 'adf.Main'
    attributes 'Build-Timestamp': new Date().format('yyyyMMddHHmmss',TimeZone.getTimeZone('UTC'));
  }
  archiveFileName = 'adf-core.jar'
}

jar.doLast { task ->
  ant.checksum file: task.archivePath
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
  archiveFileName = 'adf-core-javadoc.jar'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
  archiveFileName = 'adf-core-sources.jar'
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

build.mustRunAfter 'clean'

task start (type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'adf.Main'
  if (project.hasProperty('args')) {
    args project.args.split('\\s+')
  }
  jvmArgs '-Xms512m', '-Xmx8g'
}